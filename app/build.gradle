buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'spoon'

spoon {
    // for debug output
    debug = true

    // To run a single test class
//    className = 'fully.qualified.TestCase'

    // To run a single method in TestCase
//    methodName = 'testMyApp'

    // To execute the tests device by device */
    sequential = false

    // To grant permissions to Android M >= devices */
    grantAllPermissions = true

    if (project.hasProperty('spoonInstrumentationArgs')) {
        def list = []
        project.properties['spoonInstrumentationArgs'].split(',').each {
            list << it
        }
        instrumentationArgs = list
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        applicationId "uk.co.hd_tech.android.testingapp"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:' + rootProject.supportLibVersion;
    compile 'com.android.support:recyclerview-v7:' + rootProject.supportLibVersion;
    compile 'com.google.guava:guava:18.0'

    testCompile 'junit:junit:4.12'

    androidTestCompile 'com.android.support:support-annotations:' + rootProject.supportLibVersion;
    androidTestCompile 'com.android.support.test:runner:' + rootProject.runnerVersion;
    androidTestCompile 'com.android.support.test:rules:' + rootProject.rulesVersion;
    compile 'com.android.support.test.espresso:espresso-idling-resource:' + rootProject.espressoVersion;
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:' + rootProject.espressoVersion;
    androidTestCompile 'com.android.support.test.espresso:espresso-web:' + rootProject.espressoVersion;

//    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:' + rootProject.uiautomatorVersion;
//    androidTestCompile 'org.hamcrest:hamcrest-integration:1.3'

}
